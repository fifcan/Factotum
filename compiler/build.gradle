
/*
 * Copyright 2015 C. A. Fitzgerald
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'maven'

apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'

apply plugin: 'signing'

archivesBaseName = "factotum-compiler"

sourceCompatibility = 1.7
targetCompatibility = 1.7

test.finalizedBy(project.tasks.jacocoTestReport)

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

checkstyle{
    configFile = new File(rootDir, "checkstyle.xml")
}

description = ''

dependencies {
    compile project(':factotum')

    compile 'com.squareup:javawriter:2.5.1'
    compile 'com.google.auto.service:auto-service:1.0-rc2'
    compile 'com.google.guava:guava:18.0'
    compile 'com.squareup:javapoet:1.1.0'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.testing.compile:compile-testing:0.7'
    testCompile 'com.google.code.gson:gson:2.3.1'

    testCompile files("${System.properties['java.home']}/../lib/tools.jar")
}

if (!hasProperty('skipAndroid') ) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Factotum Compiler'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'An asynchronous task queue for Java based on the command pattern, Code gen component'
                    url 'https://github.com/riotopsys/Factotum'

                    scm {
                        connection 'scm:git:https://github.com/riotopsys/Factotum.git'
                        developerConnection 'scm:git:https://github.com/riotopsys/Factotum.git'
                        url 'https://github.com/riotopsys/Factotum'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'riotopsys'
                            name 'C. A. Fitzgerald'
                            email 'me@riotopsys.net'
                        }
                    }
                }
            }
        }
    }
}